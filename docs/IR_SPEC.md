# PYC IR Spec (prototype)

PYC is an MLIR dialect (`pyc`) intended to be a common, backend-agnostic IR for
hardware components, with multi-clock modeling and strict ready/valid streaming
semantics.

For the compiler pipeline and pass-by-pass behavior, see `docs/PIPELINE.md`.

## 1) Types

- `!pyc.clock`: a clock signal
- `!pyc.reset`: a reset signal
- Data types use MLIR integers (`i1`, `i8`, `i32`, ...).

## 2) Operations

All examples below live inside a standard MLIR `module { ... }` and use
`func.func` as the top-level “hardware module” container (prototype convention).

### 2.1 `pyc.constant`

```mlir
%c1 = pyc.constant 1 : i8
```

### 2.2 Combinational ops

```mlir
%y = pyc.add %a, %b : i8
%y = pyc.sub %a, %b : i8
%y = pyc.mul %a, %b : i8
%y = pyc.udiv %a, %b : i8
%y = pyc.urem %a, %b : i8
%y = pyc.sdiv %a, %b : i8
%y = pyc.srem %a, %b : i8
%y = pyc.select %sel, %a, %b : i1, i8, i8 -> i8
%y = pyc.and %a, %b : i8
%y = pyc.or  %a, %b : i8
%y = pyc.xor %a, %b : i8
%y = pyc.not %a : i8

%p = pyc.cmp %a, %b {predicate = "eq"} : i8, i8 -> i1
%p = pyc.cmp %a, %b {predicate = "ult"} : i8, i8 -> i1
%p = pyc.cmp %a, %b {predicate = "slt"} : i8, i8 -> i1

%lo = pyc.trunc %x : i64 -> i32
%zx = pyc.zext  %x : i8  -> i64
%sx = pyc.sext  %x : i8  -> i64

%s = pyc.extract %x {lsb = 4} : i16 -> i8
%two = pyc.constant 2 : i16
%sh = pyc.shl %x, %two : i16, i16
%lsh = pyc.lshr %x, %two : i16, i16
%ash = pyc.ashr %x, %two : i16, i16

%bus = pyc.concat(%a, %b, %c) : (i8, i16, i1) -> i25
```

### 2.2.1 `pyc.alias` (debug naming)

`pyc.alias` is a pure identity op used to attach stable debug names for codegen:

```mlir
%y = pyc.alias %x {pyc.name = "foo__my_file__L42"} : i8
```

Backends use the `pyc.name` attribute (not `name`) to avoid conflicts with other
ops that legitimately use a `name` attribute (e.g. memory instances).

### 2.2.2 `pyc.instance` (hierarchical instantiation)

`pyc.instance` instantiates another `func.func` hardware module while preserving
module boundaries (for big designs / readable codegen):

```mlir
%out_valid, %out_data = pyc.instance %clk, %rst, %in_valid, %in_data, %out_ready
  {callee = @Core__pdeadbeef, name = "core0"} : (!pyc.clock, !pyc.reset, i1, i32, i1) -> (i1, i32)
```

Attributes:

- `callee`: `FlatSymbolRefAttr` (required) referencing a `func.func`
- `name`: `StringAttr` (optional) instance name for codegen

Verifier contract:

- Operand count/types **must** match the callee’s function type inputs.
- Result count/types **must** match the callee’s function type results.

Backends emit named port connections using the callee’s `arg_names` /
`result_names` attributes.

Boundary-dynamic value params:

- Frontend may stamp:
  - `pyc.value_params = ["name0", ...]`
  - `pyc.value_param_types = ["iN"|!pyc.clock|!pyc.reset, ...]`
- These names are a subset of `arg_names`.
- Value params are runtime boundary ports only; they must not participate in
  compile-time specialization identity.

Hierarchy preservation policy:

- `pycc` strict mode (`--hierarchy-policy=strict`, default) enforces that
  frontend module symbol boundaries are preserved through lowering.
- Default C++ out-dir flows use `--inline-policy=off` so `@module` callsites
  remain explicit instance boundaries.

### 2.2.3 `pyc.assert` (simulation-only assertion)

`pyc.assert` is a simulation-only check that aborts when `cond` is false.
Backends emit it under `ifndef SYNTHESIS` in Verilog, and as a runtime check in
the C++ model.

```mlir
pyc.assert %ok {msg = "in_ready must not be asserted while full"}
```

Attributes:

- `msg`: `StringAttr` (optional) human-readable message

### 2.3 `pyc.wire` / `pyc.assign` (netlist backedges)

The prototype includes a netlist-style “wire placeholder” + explicit driver:

```mlir
%d = pyc.wire : i64
pyc.assign %d, %next : i64
```

`pyc.assign` destinations must be defined by `pyc.wire`. This is used to model
feedback loops (state machines) in SSA-based MLIR.

### 2.4 `pyc.reg` (clocked register)

```mlir
%q = pyc.reg %clk, %rst, %en, %next, %init : i8
```

Semantics (single register):

- On `posedge %clk`:
  - if `%rst` then `q := init`
  - else if `%en` then `q := next`

Common pattern (backedge):

```mlir
%d = pyc.wire : i8
%q = pyc.reg %clk, %rst, %en, %d, %init : i8
pyc.assign %d, %next : i8
```

### 2.5 `pyc.fifo` (strict ready/valid)

```mlir
%in_ready, %out_valid, %out_data =
  pyc.fifo %clk, %rst, %in_valid, %in_data, %out_ready {depth = 2} : i8
```

Handshake semantics:

- **Push** occurs when `in_valid && in_ready`
- **Pop** occurs when `out_valid && out_ready`

Notes:

- This prototype FIFO is **single-clock** (one `%clk`, one `%rst`).
- Cross-clock FIFOs should use `pyc.async_fifo` (dual-clock, strict ready/valid).

### 2.6 `pyc.comb` / `pyc.yield` (fused combinational regions)

`pyc.comb` is a codegen-oriented wrapper for fusing many small pure comb ops
into a single region:

```mlir
%y = pyc.comb(%a, %b) : (i8, i8) -> i8 {
  ^bb0(%a0: i8, %b0: i8):
    %t = pyc.add %a0, %b0 : i8
    pyc.yield %t : i8
}
```

## 3) Verilog backend (prototype)

`pycc --emit=verilog` emits Verilog:

- **Combinational ops** are typically emitted as flattened `assign` expressions (netlist style).
- **Stateful ops** instantiate the corresponding primitives from `library/verilog/`:
  - `pyc.reg` → `pyc_reg` (`library/verilog/pyc_reg.v`)
  - `pyc.fifo` → `pyc_fifo` (`library/verilog/pyc_fifo.v`)
  - `pyc.async_fifo` → `pyc_async_fifo` (`library/verilog/pyc_async_fifo.v`)
  - `pyc.byte_mem` → `pyc_byte_mem` (`library/verilog/pyc_byte_mem.v`)
  - `pyc.sync_mem` → `pyc_sync_mem` (`library/verilog/pyc_sync_mem.v`)
  - `pyc.sync_mem_dp` → `pyc_sync_mem_dp` (`library/verilog/pyc_sync_mem_dp.v`)
  - `pyc.cdc_sync` → `pyc_cdc_sync` (`library/verilog/pyc_cdc_sync.v`)

`pycc` also runs `pyc-fuse-comb`, which enables emission of flattened
Verilog `assign` statements for large purely-combinational regions.

## 4) Structured control flow (frontend temporary IR)

The Python AST/JIT frontend may emit a small subset of standard MLIR dialects:

- `scf.if` / `scf.for` (Structured Control Flow)
- `arith.constant` of type `index` (loop bounds)

These are **not** part of the stable PYC dialect contract: `pycc` runs
`pyc-lower-scf-static` to lower them into static PYC hardware ops:

- `scf.if` → `pyc.select` networks (both branches are speculated; must be side-effect-free)
- `scf.for` → fully unrolled logic (bounds must be compile-time constants)

Note: MLIR canonicalization may also introduce `arith.select` during cleanup.
`pycc` supports `arith.select` in both C++ and Verilog emission, but it
is not considered part of the stable PYC dialect surface area.
